<!--
![Screenshot of a comment on a GitHub issue showing an image, added in the Markdown, of an Octocat smiling and raising a tentacle.](/01.%20basic-sql/images/go-back-arrow.png?raw=true)
-->



[![Go to Next Course](https://raster.shields.io/badge/%3C-E69138)](/03.%20plsql/01.%20Getting%20started%20with%20PLSQL.md)
[![Go to Course Contents](https://raster.shields.io/badge/Previous_Course-E69138)](/03.%20plsql/01.%20Getting%20started%20with%20PLSQL.md)
[![Go to Course Contents](https://raster.shields.io/badge/Course_Contents-6AA84F)](/03.%20plsql/README.md)
[![Go to Course Contents](https://raster.shields.io/badge/Next_Course-3D85C6)](/03.%20plsql/03.%20Flow%20Control%20(Iterative%20Statements).md)
[![Go to Next Course](https://raster.shields.io/badge/%3E-blue)](/03.%20plsql/03.%20Flow%20Control%20(Iterative%20Statements).md)

# 02. Flow Control (Conditional Statements) 

<br>  

## Controlling Program Flow

- PL/SQL programs run sequentially from the top of the block to the bottom, unless acted upon by a control structure.
- PL/SQL provides the following functionalities with help of control structures
  
     - Ability to divert code execution in accordance with certain conditions.
     - Execute certain code repeatedly until a condition is met.
     - Jump to various sections of the block as needed.
 
- The control structures are divided into two categories.
  
     - Conditional Statements
     - Iterative Statements

<br>  

### Flow Control (Conditional Statements)

- It allows processing a portion of code depending on whether certain criteria are met or not.
- For example, IF the attained result for a module is pass THEN proceed with the next module ELSE revise the same module again.


![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/5328c191-23d4-4878-8a39-1c07c347efbf)


#### Main types of conditional control structures are

- IF-THEN
- IF-THEN-ELSE
- IF-THEN-ELSIF
- CASE
- GOTO
- NULL

<br>  



##### IF-THEN

- It is the most basic conditional evaluation.
- If a condition is met, then do something and if not met, skip the code between Then and End IF keywords and continue with the rest of the program.
- The condition must evaluate to TRUE, FALSE or NULL.

![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/0ff05c13-9c61-4a3f-8180-27c1adc9a346)

<br>  



```sql
CREATE OR REPLACE FUNCTION samenumber_func (
    arg1 IN NUMBER
    ,arg2 IN NUMBER
) RETURN VARCHAR2 IS
BEGIN
    IF arg1 = arg2 THEN 
        RETURN 'SAME NUMBER';
    END IF;
END;
```


<br>  


##### IF-THEN-ELSE

- It is an extension to IF-THEN statements.
- Allows specifying what to do if the condition evaluates to FALSE or NULL.

<br>  



![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/908443fc-8d6c-4c56-86e9-c0520ea72982)





<br>  

```sql

CREATE OR REPLACE FUNCTION evenodd_func (
    arg1 IN NUMBER
) RETURN VARCHAR2 IS
BEGIN
    IF MOD(arg1,2) = 0 THEN 
        RETURN 'EVEN';
    ELSE
        RETURN 'ODD';
    END IF;
END;

```

<br>  



##### IF-THEN-ELSIF

- It allows choosing between several alternatives.
- It provides a way to chain IF conditions together and if one is met, the rest are skipped.
- It can have an optional ELSE after ELSIF which would get executed if all the rest of the conditions are not met.
- The term is ELSIF and not ELSEIF
- An IF-THEN statement can have zero or one ELSE and it must come after all ELSIF.
- An IF-THEN statement can have zero to many ELSIF and they must come before the ELSE.
- Once an ELSIF succeeds, none of the remaining ELSIF or ELSE will be tested.

<br>  

![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/8511d008-2bb0-45cc-ac24-580e51576faf)

<br>  


```sql
CREATE OR REPLACE FUNCTION comparision_func (
    arg1 IN NUMBER
    ,arg2 IN NUMBER
) RETURN VARCHAR2 IS
BEGIN

    IF arg1 = arg2 THEN 
        RETURN 'Same Number';
    ELSIF arg1 > arg2 THEN 
        RETURN '1st Number is Greater';
    ELSE 
        RETURN '2nd Number is Greater';        
    END IF;
    
END;
```

<br>  




##### CASE

- Similar to IF-THEN-ELSE statements, CASE expression selects a result from one or more alternatives.
- The CASE expression uses a selector, an expression whose value determines which alternative to return.
- The selector is followed by one or more WHEN clauses, which are checked sequentially.
- If the first WHEN clause matches the value of the selector, then the remaining WHEN clauses are not evaluated.
- Optional ELSE clause is executed if the value of the selector does not match with any of the WHEN clauses.

<br>  

![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/64173f23-8674-4632-b264-d7635c9bf247)

<br>  

```sql
DECLARE 
   grade char(1) := 'A'; 
BEGIN 
   CASE grade 
      when 'A' then dbms_output.put_line('Excellent'); 
      when 'B' then dbms_output.put_line('Very good'); 
      when 'C' then dbms_output.put_line('Well done'); 
      when 'D' then dbms_output.put_line('You passed'); 
      when 'F' then dbms_output.put_line('Better try again'); 
      else dbms_output.put_line('No such grade'); 
   END CASE; 
END;
```


<br>  

###### Searched CASE Statement

- Searched CASE statement is similar to CASE statement but has no selector
- WHEN clauses contain search conditions that yield Boolean values.


<br>  

  ![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/e6790ebc-3665-4189-a6e8-bb0ac6c1c1e0)


<br>  

```sql
DECLARE 
   grade char(1) := 'B'; 
BEGIN 
   case  
      when grade = 'A' then dbms_output.put_line('Excellent'); 
      when grade = 'B' then dbms_output.put_line('Very good'); 
      when grade = 'C' then dbms_output.put_line('Well done'); 
      when grade = 'D' then dbms_output.put_line('You passed'); 
      when grade = 'F' then dbms_output.put_line('Better try again'); 
      else dbms_output.put_line('No such grade'); 
   end case; 
END;
```

<br>  



##### GOTO

- A GOTO statement provides an unconditional jump from the GOTO to a labelled statement in the same sub program.
- GOTO provides the ability to jump through a program from one place to another.
- It is better to have a limited usage of GOTO in program code.
- When a jump is proposed using GOTO , it is associated with an appropriate label.
- The GOTO statement branches to a label unconditionally.
- The label must be unique within its scope and must precede an executable statement.
- When executed, the GOTO statement transfers the control to the labelled statements or block.
- The labelled statement or block can be down or up in the sequence of statements.
- The labels are enclosed between angular brackets << and >>

<br>  

```sql
BEGIN
  GOTO second_message;

  <<first_message>>
  DBMS_OUTPUT.PUT_LINE( 'Hello' );
  GOTO the_end;

  <<second_message>>
  DBMS_OUTPUT.PUT_LINE( 'PL/SQL GOTO Demo' );
  GOTO first_message;

  <<the_end>>
  DBMS_OUTPUT.PUT_LINE( 'and good bye...' );

END;
```

<br>  

![image](https://github.com/venkatdurgempudi/SQL/assets/15828692/21e2c613-a05a-4453-a133-f273ebda6118)

<br>  

##### GOTO statement restrictions

- ###### you cannot use a GOTO statement to transfer control into an IF, CASE or LOOP statement, the same for the sub-block.

<br>  

```sql
DECLARE 
  n_sales NUMBER;
  n_tax NUMBER;
BEGIN 
    GOTO inside_if_statement;
    IF n_sales > 0 THEN
      <<inside_if_statement>>
      n_tax  := n_sales * 0.1;
    END IF;
END;
```

<br>  


```
> PLS-00375: illegal GOTO statement; this GOTO cannot branch to label 'INSIDE_IF_STATEMENT'
```

<br>  

- ###### you cannot use a GOTO statement to transfer control from one clause to another in the IF statement e.g., from IF clause to ELSIF or ELSE clause, or from one WHEN clause to another in the CASE statement.


<br>  

```sql
DECLARE
  n_sales      NUMBER;
  n_commission NUMBER;
BEGIN
  n_sales := 120000;
  IF n_sales      > 100000 THEN
    n_commission := 0.2;
    GOTO zero_commission;
  elsif n_sales   > 50000 THEN
    n_commission := 0.15;
  elsif n_sales   > 20000 THEN
    n_commission := 0.1;
  ELSE
    <<zero_commission>>
    n_commission := 0;
  END IF;
END;
```

<br>  

```
> PLS-00375: illegal GOTO statement; this GOTO cannot branch to label 'ZERO_COMMISSION'
```

<br>  

- ###### you cannot use a GOTO statement to transfer control out of a subprogram or into an exception handler.

- ###### you cannot use a GOTO statement to transfer control from an exception handler back into the current block.


<br>  



##### NULL  

- PL/SQL uses NULL statement when there is nothing to execute.
- The NULL statement does nothing, and passes control to the next statement.

<br>  



```sql
CREATE PROCEDURE request_for_aproval( 
    customer_id NUMBER 
)
AS
BEGIN
  NULL;
END;
```

<br>  

<br>  

<br>  












